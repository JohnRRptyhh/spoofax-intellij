plugins {
    id "org.jetbrains.intellij" version "0.0.28"
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'maven'
//apply plugin: 'checkstyle'
apply plugin: 'org.jetbrains.intellij'

group 'org.metaborg'
version '1.5.0-SNAPSHOT'
sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenLocal()
    mavenCentral()
}

// FIX: Circular dependency due to adding test class dependencies
// https://issues.gradle.org/browse/GRADLE-2470
subprojects {
    apply plugin: 'java'
}

configurations {
    provided
}

sourceSets.main.compileClasspath += configurations.provided

idea {
    module {
        inheritOutputDirs = true
        scopes.PROVIDED.plus += configurations.provided
    }
}

eclipse {
    classpath {
        plusConfigurations += configurations.provided
    }
}

intellij {
    version 'IC-15.0.1'
    //plugins 'maven'
}

// Task to allow a debugger to be attached to port 5005 for debugging the JPS plugin.
// https://github.com/JetBrains/gradle-intellij-plugin/issues/23
afterEvaluate {
    task("debugIdea", type: org.jetbrains.intellij.RunIdeaTask, group: 'intellij', dependsOn: 'prepareSandbox', description: 'Runs Intellij IDEA with installed plugin and debugger support for JPS on port 5005.') {
        jvmArgs += ["-Dcompiler.process.debug.port=5005"]
    }
}

dependencies {
    // Main dependencies
    compile project(':org.metaborg.settings')
    compile project(':org.metaborg.settings.jackson')

    compile('org.metaborg:org.metaborg.core:1.5.0-SNAPSHOT') {
        exclude group: 'org.slf4j'
    }
    compile('org.metaborg:org.metaborg.spoofax.core:1.5.0-SNAPSHOT') {
        exclude group: 'org.slf4j'
    }
    compile('org.metaborg:org.metaborg.spoofax.generator:1.5.0-SNAPSHOT') {
        exclude group: 'org.slf4j'
    }
    compile('org.metaborg:org.metaborg.spoofax.meta.core:1.5.0-SNAPSHOT') {
        exclude group: 'org.slf4j'
    }

    compile 'com.google.code.findbugs:jsr305:3.0.0'
    compile 'com.google.guava:guava:18.0'
    compile 'org.metaborg:make-permissive:1.5.0-SNAPSHOT'
    compile 'org.apache.ant:ant:1.9.6'
    compile 'org.apache.commons:commons-vfs2:2.0'
    compile 'com.google.inject:guice:3.0'
    compile 'com.google.inject.extensions:guice-assistedinject:3.0'
    compile 'org.javassist:javassist:3.20.0-GA'
    compile 'org.slf4j:slf4j-api:1.7.10'
    compile 'org.slf4j:slf4j-simple:1.7.10'

    // Test dependencies
    testCompile project(':org.metaborg.settings').sourceSets.test.output
    testCompile project(':org.metaborg.settings.jackson').sourceSets.test.output
    testCompile group: 'junit', name: 'junit', version: '4.11'
}

uploadArchives {
    repositories {
        mavenDeployer {
            mavenLocal()
        }
    }
}
